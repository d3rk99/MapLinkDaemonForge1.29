modded class MissionServer extends MissionBase
{
	bool IsMapLinkAdmin(PlayerIdentity identity)
	{
		if (!identity)
		{
			return false;
		}

		// Check UID
		if (GetMapLinkConfig().IsMapLinkAdmin(identity.GetId()))
		{
			return true;
		}

		// Check Steam
		if (GetMapLinkConfig().IsMapLinkAdmin(identity.GetPlainId()))
		{
			return true;
		}

		return false;
	}

	ref map<string, ref PlayerDataStore> m_PlayerDBQue = new map<string, ref PlayerDataStore>;
	string m_worldname;
	int MapLinkConfigRefreshTimer = 0;
	static int MAPLINK_CONFIG_REFRESH_TIME = 90000;
	
	override void OnMissionStart()
	{
		super.OnMissionStart();
		GetGame().GetWorldName(m_worldname);
		MLLog.Info("On Mission Start World: " + m_worldname + " Server: " + UApiConfig().ServerID);
	}

	void LoadPlayerFromUApiDB(int cid, int status, string oid, string data)
	{	
		ML129Log("UniversalAPI load response cid=" + cid + " uid=" + oid + " status=" + status);
      	if (status == UAPI_SUCCESS)
		{  
			//If its a success
			PlayerDataStore dataload;

			if (UApiJSONHandler<PlayerDataStore>.FromString(data, dataload))
			{
				MLLog.Debug("LoadPlayerFromDB - Success ID:" + cid + " - GUID: " + oid);

				if (dataload.IsValid() && dataload.GUID == oid)
				{
					ML129Log("UniversalAPI load success uid=" + oid + " sourceServer=" + dataload.m_Server + " transferPoint=" + dataload.m_TransferPoint);
					MLLog.Info("Add Player to PlayerQue " + dataload.m_Name + "(" + dataload.GUID + ") Health:  " + dataload.m_Health + " PlayTime: " + dataload.m_TimeSurvivedValue + " Server: " + dataload.m_Server + " TransferPoint: " + dataload.m_TransferPoint);
					m_PlayerDBQue.Set(oid, PlayerDataStore.Cast(dataload));
				} else 
				if (m_PlayerDBQue.Contains(oid)) 
				{ 
					//This shouldn't be needed any more
					ML129Warn("UniversalAPI load fail invalid player data uid=" + oid);
					MLLog.Log("Player is dead or data invlaid Removing Player from Queue " + oid);
					m_PlayerDBQue.Remove(oid);
				}

				return;
			}

			ML129Warn("UniversalAPI load fail parse error cid=" + cid + " uid=" + oid);
			MLLog.Err("LoadPlayerFromDB - Error Loading Data from the API - ID:" + cid + " - GUID: " + oid);
			return;
      	} 

		if (status == UAPI_EMPTY)
		{
			ML129Warn("UniversalAPI load fail empty response cid=" + cid + " uid=" + oid);
			MLLog.Info("LoadPlayerFromDB - Empty Response - ID:" + cid + " - GUID: " + oid);
			return;
		}

		ML129Warn("UniversalAPI load fail api status=" + status + " uid=" + oid);
        MLLog.Err("LoadPlayerFromDB - API CALL FAILED - CHECK OVER YOUR CONFIGS AND ENSURE THAT THE API IS RUNNING ID: " + oid + " S:" + status);
	}

	override void OnClientPrepareEvent(PlayerIdentity identity, out bool useDB, out vector pos, out float yaw, out int preloadTimeout)
	{
		int CurrentTime = GetGame().GetTime();

		if (CurrentTime > MapLinkConfigRefreshTimer)
		{
			MapLinkConfigRefreshTimer = CurrentTime + MAPLINK_CONFIG_REFRESH_TIME;
			GetMapLinkConfig().Load();
		}

		if (identity)
		{
			int cid = UApi().db(PLAYER_DB).Load("MapLink", identity.GetId(), this, "LoadPlayerFromUApiDB");	
			ML129Log("UniversalAPI load start cid=" + cid + " uid=" + identity.GetId() + " steam64=" + identity.GetPlainId() + " server=" + UApiConfig().ServerID);
			MLLog.Info("Requesting Player Data from DataBase Call ID:" + cid + " - GUID: " + identity.GetId());
			//NotificationSystem.SimpleNoticiation("Requesting your login player Data From the API", "Notification","Notifications/gui/data/notifications.edds", -16843010, 10, identity);
		} else 
		{
			ML129Warn("UniversalAPI load start failed uid=NULL");
			MLLog.Info("Requesting Player Data from DataBase - GUID: NULL");
		}

		super.OnClientPrepareEvent(identity, useDB, pos, yaw, preloadTimeout);
	}
	
	override void OnEvent(EventType eventTypeId, Param params) 
	{		
		if (eventTypeId == ClientNewEventTypeID) 
		{
			ClientNewEventParams newParams;
			Class.CastTo(newParams, params);

			//If the player was created, end if not spawn a new fresh spawn
			//Also need to spawn fresh spawns to be able to kick them with the redirect or they will get kick with a player not created message instead
			if (UApiOnClientNewEvent(newParams.param1, newParams.param2, newParams.param3))
			{ 
				MLLog.Info("Player " + PlayerIdentity.Cast(newParams.param1).GetId() +" Was Created from API");
				return;
			}

			MLLog.Info("Player " + PlayerIdentity.Cast(newParams.param1).GetId() + " Spawning Fresh");
		}

		super.OnEvent(eventTypeId, params);
	}
		
	bool UApiOnClientNewEvent(PlayerIdentity identity, vector pos, ParamsReadContext ctx)
	{
		MLLog.Info("MapLink :: UApiOnClientNewEvent @ " + pos);

		PlayerDataStore playerdata;	
		
		if (identity && m_PlayerDBQue.Contains(identity.GetId()) && m_PlayerDBQue.Find(identity.GetId(), playerdata) && playerdata.IsValid()) 
		{
			pos = "0 0 0";
			vector ori = "0 0 0";
			UApiServerData serverData;
			string transferPoint =  playerdata.m_TransferPoint;
			string fromServerName = playerdata.m_Server;

			if (IsMapLinkAdmin(identity) && fromServerName != UApiConfig().ServerID && transferPoint == "")
			{
				MLLog.Info("Admin " + identity.GetId() + " bypassed server redirection. Allowing login directly to current server.");
				fromServerName = UApiConfig().ServerID;
				playerdata.m_TransferPoint = "";
				pos = GetGame().ConfigGetVector(string.Format("CfgWorlds %1 centerPosition", GetGame().GetWorldName()));
				pos[1] = GetGame().SurfaceY(pos[0], pos[2]) + 0.1; // Place on surface
			}
			
			if (!playerdata.IsAlive() || playerdata.IsUnconscious())
			{
				UApiServerData CurServerData = UApiServerData.Cast(GetMapLinkConfig().GetServer(UApiConfig().ServerID));

				if (CurServerData && CurServerData.RespawnServer && CurServerData.RespawnServer != "" && CurServerData.RespawnServer != UApiConfig().ServerID)
				{
					serverData = UApiServerData.Cast(GetMapLinkConfig().GetServer(CurServerData.RespawnServer));
					if (serverData)
					{
						NotificationSystem.Create(new StringLocaliser("Map Link"), new StringLocaliser("#STR_MapLink_Redirect - " + CurServerData.RespawnServer), "set:maplink_icons image:redirect", -16843010, 16, identity);
						GetRPCManager().SendRPC("MapLink", "RPCRedirectedKicked", new Param1<UApiServerData>(serverData), true, identity);
						ML129Log("redirect sent uid=" + identity.GetId() + " target=" + CurServerData.RespawnServer + " reason=dead_or_unconscious");
					}
				}

				MLLog.Info("Player " + identity.GetId() +" IsAlive: " + playerdata.IsAlive() + " IsUnconscious: " + playerdata.IsUnconscious() + " IsRestrained: " + playerdata.IsRestrained()  + " on the API, spawning them fresh");
				MLLog.Debug("Removing Player from Queue " + identity.GetId());
				m_PlayerDBQue.Remove(identity.GetId());
			    return false;
			}

			MLLog.Log("Spawning player " + identity.GetId() + " on: " + UApiConfig().ServerID + " World: " + m_worldname + " at " + transferPoint);
			MapLinkSpawnPointPos pointPos;

			// Treat any non-empty transferPoint as "arrival in progress"
			if (transferPoint != "")
			{
				if (Class.CastTo(pointPos, GetMapLinkConfig().SpawnPointPos(transferPoint)))
				{
					// This server is the intended receiver; spawn at arrival
					pos = pointPos.Get();
					ori = pointPos.GetOrientation();
				}
				else 
				if (fromServerName != UApiConfig().ServerID)
				{
					// Not configured to receive here; send them to the saved destination
					serverData = UApiServerData.Cast(GetMapLinkConfig().GetServer(fromServerName));
					if (serverData)
					{
						GetRPCManager().SendRPC("MapLink", "RPCRedirectedKicked", new Param1<UApiServerData>(serverData), true, identity);
						ML129Log("redirect sent uid=" + identity.GetId() + " target=" + fromServerName + " reason=arrival_point_missing");
					}
					else
					{
						ML129Warn("redirect failed uid=" + identity.GetId() + " target=" + fromServerName + " reason=server data missing");
					}
					MLLog.Info("MapLink: arrival point '" + transferPoint + "' not on this server; redirecting to " + fromServerName);
					m_PlayerDBQue.Remove(identity.GetId());
					return false;
				}
				// else: no spawn point and no different server—fall through to default spawn (should not happen if config is consistent)
			}

			if (fromServerName != UApiConfig().ServerID)
			{
				serverData = UApiServerData.Cast(GetMapLinkConfig().GetServer(fromServerName));
				if (serverData) 
				{
					NotificationSystem.Create(new StringLocaliser("Map Link"), new StringLocaliser("#STR_MapLink_Redirecting"), "set:maplink_icons image:redirect", -16843010, 16, identity);
					GetRPCManager().SendRPC("MapLink", "RPCRedirectedKicked", new Param1<UApiServerData>(serverData), true, identity);
					ML129Log("redirect sent uid=" + identity.GetId() + " target=" + fromServerName + " reason=wrong_server");
				}

				m_PlayerDBQue.Remove(identity.GetId());
				return false;
			}

			// We are on the correct server: finish the arrival, if any
			if (transferPoint != "")
			{
				pointPos;
				if (Class.CastTo(pointPos, GetMapLinkConfig().SpawnPointPos(transferPoint))) 
				{
					pos = pointPos.Get();
					ori = pointPos.GetOrientation();
				} else 
				{
					MLLog.Err("Arrival point '" + transferPoint + "' not configured on " + UApiConfig().ServerID + "; using default spawn.");
					pos = GetGame().ConfigGetVector(string.Format("CfgWorlds %1 centerPosition", GetGame().GetWorldName()));
					pos[1] = GetGame().SurfaceY(pos[0], pos[2]) + 0.1;
				}
			}

			PlayerBase player = PlayerBase.Cast(PlayerDataStore.Cast(playerdata).CreateWithIdentity(PlayerIdentity.Cast(identity), pos));
			ML129Log("inventory restore start uid=" + identity.GetId() + " source=" + fromServerName + " target=" + UApiConfig().ServerID + " transferPoint=" + transferPoint);
			if (!player)
			{
				ML129Warn("restore failure reason: CreateWithIdentity returned null uid=" + identity.GetId() + " type=" + playerdata.m_Type);
				m_PlayerDBQue.Remove(identity.GetId());
				return false;
			}
			GetGame().SelectPlayer(identity, player);
			InvokeOnConnect(player, identity);
			SyncEvents.SendPlayerList();
			ControlPersonalLight(player);
			SyncGlobalLighting(player);
			PlayerDataStore.Cast(playerdata).SetupPlayer(player, pos, ori);
			ML129Log("inventory restore end uid=" + identity.GetId());

			if (fromServerName != UApiConfig().ServerID && transferPoint != "") 
			{
				int protectionTime = GetMapLinkConfig().GetProtectionTime(transferPoint);
				MLLog.Info("Adding Protection to " + playerdata.GUID + "  at " + transferPoint + " for " + protectionTime);
				if (protectionTime > 0)
				{
					GetGame().GetCallQueue(CALL_CATEGORY_SYSTEM).Call(player.UpdateMapLinkProtection, protectionTime);
				}
			}

			GetGame().GetCallQueue(CALL_CATEGORY_SYSTEM).CallLater(player.SavePlayerToUApi, 100);
			MLLog.Debug("Removing Player from Queue " + identity.GetId());
			m_PlayerDBQue.Remove(identity.GetId());
			
			return true;
		}

		return false;
	}
	
	override void HandleBody(PlayerBase player)
	{
		if (player && player.IsBeingTransfered())
		{
			string cleanupUid = "NULL";
			if (player.GetIdentity())
			{
				cleanupUid = player.GetIdentity().GetId();
			}
			ML129Log("source player cleanup HandleBody kill uid=" + cleanupUid);
			MLLog.Debug("HandleBody IsBeingTransfered Killing Player"); //Fail Safe
			player.SetAllowDamage(true);
			player.SetHealth("", "Health", 0);
			player.SetHealth("", "", 0);
		}

		super.HandleBody(player);

		if (player && (player.MapLinkShoudDelete() || player.IsBeingTransfered())) 
		{
			string deleteUid = "NULL";
			if (player.GetIdentity())
			{
				deleteUid = player.GetIdentity().GetId();
			}
			ML129Log("source player cleanup HandleBody delete uid=" + deleteUid);
			MLLog.Debug("HandleBody IsBeingTransfered Removing body");
			// remove the body
			player.Delete();	
		}
	}
}
